plugins {
    id 'com.gradle.plugin-publish' version '0.9.10'
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

version = '0.0.9'
group = 'io.totokaka.gradle.pyenv'

repositories {
    jcenter()
}


dependencies {
    compile localGroovy()

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
}

sourceSets {
    integrationTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
    functionalTest {
        groovy.srcDir file('src/functTest/groovy')
        resources.srcDir file('src/functTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

gradlePlugin {
    plugins {
        simplePython {
            id = 'io.totokaka.simple-python'
            implementationClass = 'io.totokaka.gradle.pyenv.plugins.simplePython.SimplePythonPlugin'
        }
        simplePythonBase {
            id = 'io.totokaka.simple-python-base'
            implementationClass = 'io.totokaka.gradle.pyenv.plugins.base.SimplePythonBasePlugin'
        }
    }

    testSourceSets sourceSets.functionalTest
}

pluginBundle {
    website = 'https://github.com/totokaka/gradle-pyenv'
    vcsUrl = 'https://github.com/totokaka/gradle-pyenv.git'

    plugins {
        simplePython {
            id = 'io.totokaka.simple-python'
            displayName = 'simple-python'
            description = 'Simple gradle plugin for building python projects'
            tags = ['python']
        }
        simplePythonBase {
            id = 'io.totokaka.simple-python-base'
            displayName = 'simple-python-base'
            description = 'The base plugin for simple-python (conventions)'
            tags = ['python']
        }
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter test
}
check.dependsOn integrationTest

task functionalTest(type: Test) {
    description = 'Runs the function tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test, integrationTest
}
//check.dependsOn functionalTest

